all_characters = ? all visible ansii characters ?;
letter = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" 
      | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" 
      | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A"
      | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
      | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S"
      | "T" | "U" | "V" | "W" | "X" | "Y" | "Z";
terminal = '"', { all characters }, '"';

digit_excluding_zero = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
digit = "0" | digit_excluding_zero;
natural_number = digit_excluding_zero, { digit };
number = "0" | [ "-" ], natural_number, [ ".", digit, { digit } ] | [ "-" ], "0", ".", digit, { digit };

atom = "@", ident;
bool = "true" | "false";

constant = terminal | number | bool | list | map;

ident = ( letter | "_" ), { letter | digit | "_" };

key_value_pair = terminal, ":", expression;
map = "{", [ key_value_pair, { ",", key_value_pair }, [ "," ] ], "}";
list = "[", [ expression, { ",", expression }, [ "," ] ], "]";

index = "[", expression, "]";
call = "(", [ expression, { ",", expression } ], ")";

block = "{", { statement }, "}";

while_loop = "while", "(", expression, ")", statement;
for_loop = "for", "(", expression, ";", expression, ";", expression, ")", statement;
if = "if", "(", expression, ")", statement, { "elif", "(", expression, ")", statement }, [ "else", statement ];
var_decl = "var", ident, "=", expression;
return = "return", [ expression ], ";";

parens = "(", expression, ")";

ex_term = ident | constant | parens | atom;
ex_postfix = ex_term, { ( index | call | "++" | "--" ) } | ex_term;
ex_prefix = ( "!" | "++" | "--" | "-" ), ex_prefix | ex_postfix;
ex_mul = ex_prefix, ( "*" | "/" | "%" ), ex_mul | ex_prefix;
ex_add = ex_mul, ( "+" | "-" ), ex_add | ex_mul;
ex_compare = ex_add, ( "<" | ">" | "<=" | ">=" ), ex_compare | ex_add;
ex_concat = ex_compare, "#", ex_concat | ex_compare;
ex_equals = ex_concat, ( "==" | "!=" ), ex_equals | ex_concat;
ex_and = ex_equals, "&&", ex_and | ex_equals;
ex_or = ex_and, "||", ex_or | ex_and;
expression = ex_or, ( "*=" | "/=" | "+=" | "-=" | "#=" ), expression | ex_or;

statement = [ expression ], ";" | var_decl | block | while_loop | for_loop | if | return;

func = "function", ident, "(", [ ident, { ",", ident } ], ")", block;
program = { func };